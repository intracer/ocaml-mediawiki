(**
  {5 Prop queries}
*)

open Utils
open WTypes
open Datatypes

(** {6 Page retrieving} *)

val of_titles : session -> string list -> (string * page) Enum.t
(** [of_titles s titles] associates to every title in [titles] the corresponding
    page. If the title is invalid or missing, it is absent from the answer. *)

val of_pageids : session -> page Id.t list -> (page Id.t, page) Map.t Call.t
(** [of_pageids s pageids] associates to every page id in [pageids] the 
    corresponding page. If the id is invalid or missing, it is absent from the 
    answer. *)

val normalize : session -> string list -> (string, title) Map.t Call.t
(** [normalize s titles] returns the canonical title associated to a given 
    string, regardless of the existence of the page. If a title is invalid, it
    is absent from the answer. *)

(** {6 Revision retrieving} *)

val revisions : session -> ?fromid:revision Id.t -> ?uptoid:revision Id.t -> ?fromts:timestamp ->
  ?uptots:timestamp -> ?order:order -> ?usrfilter:user_filter -> ?limit:int ->
  page -> revision Enum.t
(** Returns the list of revisions of a given page. Empty result if the page is 
  invalid. *)

val of_revids : session -> revision Id.t list -> (revision Id.t, revision) Map.t Call.t
(** [of_revids s revids] associates to every revision id in [revids] the 
    corresponding revision. If the id is invalid or missing, it is absent from
    the answer. *)

(** {6 Content} *)

val content : session -> revision list -> (revision Id.t, string) Map.t Call.t
(** Associates to every revision its content by id.
    If a revision is invalid, it is not present in the answer. *)

(** {6 Diffs} *)

val diff : session -> revision Id.t -> revision relative_id -> diff Call.t

(**
  {6 Various enumerations}

  These are various enumerations generated by prop queries.
*)

val links : session -> ?ns:namespace list -> ?limit:int -> page -> title Enum.t
(** Returns the list of link titles from a given page. *)

val langlinks : session -> ?limit:int -> page -> langlink Enum.t
(** Return the list of interwiki links from a given page. *)

val images : session -> ?limit:int -> page -> title Enum.t
(** Returns the list of image titles used on a given page. *)

val templates : session -> ?ns:namespace list -> ?limit:int -> page -> title Enum.t
(** Returns the list of template titles used on a given page. *)

val categories : session -> ?limit:int -> page -> string Enum.t
(** Returns the list of categories to which pertains a given page. *)

val external_links : session -> ?limit:int -> page -> string Enum.t
(** Returns the list of external links contained in a given page. *)

(** {6 Debugging} *)

val dummy_title : title
val dummy_page : page Id.t -> page
val dummy_revision : revision Id.t -> revision
